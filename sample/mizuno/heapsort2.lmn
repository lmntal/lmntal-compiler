ret=sort(gen(10)).

R=gen(N) :- N > 0 | R=[integer.rnd(100)|gen(N-1)].
R=gen(N) :- N = 0 | R=[].

R=sort([D|L]), heap({$p,@p}) :- int(D) | R=sort(L), heap({insert(D),$p,@p}).
R=sort([]   ), heap({$p,@p}) :- R=sort2, heap({$p,@p}).
R=sort2,       heap({$p,@p}) :- R=[remove({$p,@p})|sort2].
R=[remove({root=R,leaves([R|L],L),@p}/)|sort2] :- R=[].

% 以下ヒープ

R=remove({root=c(C1,C2,VT), leaves(F,L), L2=[R1,R2|L], c(R1,R2,VL,P), $p[C1,C2,F,L2,P|*X], @p}/)
	:- int(VT), int(VL) | R=VT, heap({root=c(C1,C2,VL), leaves([P|F],L2), $p[C1,C2,F,L2,P|*X], @p}).
R=remove({root=c(R1,R2,VT), leaves([R1,R2|L],L), @p}/)
	:- int(VT) | R=VT, heap({root=R, leaves([R|L],L), @p}).

heap({
% c(Child1, Child2, Value, Parent)
root=R. leaves([R|L], L).

c(C1,c(C2,C3,V2),V1,P) :- V2 < V1 | c(C1,c(C2,C3,V1),V2,P).
c(c(C2,C3,V2),C1,V1,P) :- V2 < V1 | c(c(C2,C3,V1),C1,V2,P).

insert(V), root=R, leaves([R|L], L) :- root=c(R1,R2,V), leaves([R1,R2|L], L).
insert(V), leaves([R|T], L) :- R=c(R1,R2,V), leaves(T, L2), L=[R1,R2|L2].
}).
